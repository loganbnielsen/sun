type execution_outcome =
  | Applied of { namespace: string; name: string; image: string; consumer_groups: string list }
  | Emitted of { file: string }
  | Dry_run
  | Failed of { phase: string; message: string }

let deploy_state_configmap_name workspace =
  Printf.sprintf "sun-deploy-state-%s" workspace

let load_deployed_groups workspace =
  let name = deploy_state_configmap_name workspace in
  match Sun_cli_kubectl.get ~resource:"configmap" ~name ~namespace:"default"
          ~output:"jsonpath={.data.consumer_groups}" with
  | Error _ -> []
  | Ok r when r.Sun_cli_process.exit_code <> 0 -> []
  | Ok r ->
    String.split_on_char '\n' r.Sun_cli_process.stdout
    |> List.map String.trim
    |> List.filter (fun s -> s <> "")

let save_deployed_groups workspace groups =
  let name = deploy_state_configmap_name workspace in
  let value = String.concat "\n" groups in
  let apply_json = Printf.sprintf
    {|{"apiVersion":"v1","kind":"ConfigMap","metadata":{"name":"%s","namespace":"default"},"data":{"consumer_groups":"%s"}}|}
    (String.escaped name) (String.escaped value)
  in
  let path = Filename.temp_file "sun-state-" ".json" in
  let oc = open_out path in
  output_string oc apply_json;
  close_out oc;
  ignore (Sun_cli_kubectl.apply ~file:path);
  (try Sys.remove path with _ -> ())

let record_outcome workspace outcome =
  match outcome with
  | Applied { consumer_groups; _ } -> save_deployed_groups workspace consumer_groups
  | Emitted _ | Dry_run | Failed _ -> ()

let removed_consumer_groups ~prev ~next =
  List.filter (fun g -> not (List.mem g next)) prev
